    subroutine main(ip,aa,dL,dC)
!   Связанные линии с горизонтально-лицевой связью (СЛ ГЛС) 
!   BSCL (Broad-Side Coupled Lines) 
!   Ширина бруска-подложки = ширине нижней полоски W2
 
	implicit complex*16(c,w,z), real*8(a-b,d-h,o-v,x-y)
    dimension aa(8),dL(4),dC(4),bC(4)   !  Массивы dC и bC  д.б. согласованы (4)
    dimension z1(11),z2(11), betam(11), qwork(125) ! nptsq*(2n+3)=5*(2*11+3)=125 
	dimension z3c(4),z4c(4), z3p(4),z4p(4), betam4(4), qwork4(55)
    dimension z3(11),z4(6),x4(6)
	dimension  z1in(7),z2in(7), z4cin(7),z4pin(7)   ! линии-границы раздела диэл.
	dimension x4cin(7),x4pin(7),y4cin(7),y4pin(7)   ! линии-границы раздела диэл.	
	dimension x4ci(5), x4pi(5), y4ci(5), y4pi(5)  ! линии-границы раздела диэл.

	zinf = (1.d20,1.d20)      ! "машинная" бесконечность
	zero = (0.d0, 0.d0)		  ! нуль
	zr = (1.d0, 0.d0)		  ! вещест.единица  
	zi = (0.d0, 1.d0)		  ! мнимая единица	
    pi = dacos(-1.d0)		  ! число пи
!	Исходные данные:
    ip = ip	   ! Индекс печати = -2,-1,0,1. Если ip=0, то всё печатаем.
    n  = aa(1) ! количество линий
    aw1= aa(2) ! ширина верхней линии 
	aw2= aa(3) ! ширина нижней линии
    h1 = aa(4) ! высота верхнего бруска-подложки 
    h2 = aa(5) ! высота нижней зазора
	t  = aa(6) ! толщина полосок (линий)
    e1 = aa(7) ! диэл.проницаем. бруска-подложки 
    e2 = aa(8) ! диэл.проницаем. среды
	inn = size(z1in) ! Кол-во точек интереса на линии-границе раздела диэлектриков z1in
	if(ip==0)print*,'inn=',inn
!------------------------------------------------------------------------------
! Инициализация для внутренего тестирования:
! n=2; aw1 =2; aw2= 4; h1=1;  h2=1. ; t=.035; e1=16; e2=1 ! "Классический" ТрНМ
! n=2; aw1=.4; aw2=.8; h1=.2; h2=.25; t=.015; e1=20; e2=1 ! Керамический ТрН мост
!==============================================================================
    nn = 11    ! Общее кол-во вершин в исх. многоугол.(вкл. доп.точку, нуль, беcконеч.)
    iguess = 1
	zh2=zi*h2; zh2t=zi*(h2+t); zh2th1=zi*(h2+t+h1); zh2th1t=zi*(h2+t+h1+t) ! вертикали
!------------------------------------------------------------------------------
! Координаты вершин и углы при вершинах 11-угольной исходной области 
	z1(1) =          zh2th1t;	betam(1) =  0.5
    z1(2) = aw1/2. + zh2th1t;	betam(2) =  1.5
    z1(3) = aw1/2. + zh2th1 ;	betam(3) =  1.5
    z1(4) =			 zh2th1;	betam(4) =  0.5
    z1(5) =			 zh2t;		betam(5) =  0.5
    z1(6) = aw2/2. + zh2t ;		betam(6) =  1.5
    z1(7) = aw2/2. + zh2;		betam(7) =  1.5
	z1(8) =			 zh2;		betam(8) =  0.5
	z1(9) = 0;					betam(9) =  0.5
	z1(10)= zinf;				betam(10)= -0.5
	z1(11)= 2*zh2th1t;			betam(11)=  1  
								betam = betam - 1. ! Вычисление "правильных" углов
! Доп. точка #11 для завешения (т.к. Inf не м.б. последней)
!------------------------------------------------------------------------------
! Определение в z1 точек раздела емкостей Cс=С01+С02 (синфаз.) и Cр=С01+С12 (противофаз.)
	z1_45 = zi*(h2+t+h1/2.); ! точка раздела емкостей Cc = С01 + С02 при синфаз.возбуд.
	z1_89 = zi*h2/2.;        ! тчка раздела емкостей Cр = С01 + С12 при противофаз.возбуд.
	z1_pt1 = aw2/2. +zh2th1					     ! Угловая точка диэлектрического бруска
	z1_pt2 = (z1(3)+z1_pt1)/2.					 ! Средняя точка горизонтал.границы бруска
	z1_pt3 = aw2/2. +z1_45						 ! Средняя точка вертикал.границы бруска 
! Линия угловой границы диэл. бруска z1in
	z1in =(/z1(3), z1_pt2, z1_pt1, z1_pt3, z1(6), z1_45, z1_89/)  
! Линия раздела диэлектриков z1in в исходной области z1
	if(ip==0) then; print*,'  Diel.interface z1in:'; write(*,77)(i, z1in(i),i=1,inn) 
77	format(i3,1x,'(',f9.6,',',f9.6,')'); endif 
! Контрольная печать исходных данных
   	if(ip==0) then
	  print '('' nn ='', i2)', nn
	  print*,'BSCL geometry'
		do 10 i = 1,nn
10		write(6,110) i, z1(i)
110		format(' z1= w(k)=', i3, 2x, f7.3, 1x, f6.3)
	endif
!==========================================================================
! Задание конформ. центра (z10 = WC) в исх. многоугол. z1
    z10 = dcmplx((aw1+aw2)/2., h2+t+h1/1.1) ! Найдено эвристич./эмпирич.
	z10 = dcmplx((aw1+aw2)/2., h2+t)		! Уточнено эвристич./эмпирич.для w(2,4)h(1,1)..
	z10 = dcmplx((aw1+aw2)/2.4, h2+t*2)	    ! Уточнено эвристич./эмпирич.для w(9,5)h(.9,2.1)
!	z10 = dcmplx((aw1+aw2)/2.5, h2+t)   ! Уточнено эвристич./эмпирич.:)кроме w=.3,.8 h=.2,.25
!	z10 = dcmplx((aw1+aw2)/2.1, h2+t*1.1)! Уточнено эвристич./эмпирич.:)кроме w=4.1,2.5 h=1,1
	z10 = dcmplx((aw1+aw2)/2.2, h2+t*1.5)! Уточнено эвристич./эмпирич.:))) Универсал!


		if(ip.eq.0) print '(''z10= wc = ('', 2e13.5, '')'' )', z10
! Вычисляет узлы и веса для параметрической задачи (влияет на точность):
    nq = 5		! обычно nptsq = 2..6 
	   call qinit(nn,betam,nq,qwork)
! Начальное приближение отображения c круга z2 на заданный многоугольник z1
    do 11 k = 1,nn 
11  z2(k) = exp(dcmplx(0.d0, k-nn))  
    tol = 1.d-6	! Точность tol =eps= 10.**(-(nq+1))
!	iprint=-2	! Индекс печати = -2,-1,0,1
	iguess=1
! Решение задачи нахождения параметров интеграла Кристоффеля-Шварца SC
	   call scsolv(ip,iguess,tol,errest, nn, c, z2, z10, z1, betam,nq,qwork)
!	z1(k)= w1(k) - вершины многоугольника (в оригинале было обозначено как w(k))
!	z2(k)= z1(k) - 1-я круговая канон. область (в оригинале обозначено как z(k))
!   z3(k)= z2(k) - 2-я круговая каноническая область  
!   z4(k)= w2(k) - финальная прямоугольная область
!==========================================================================
! Расчет полуструктуры СЛ сразу (без режимов Сс,Ср) с использ. GHIONE2 (воздух)
		z3 = (z2*dconjg(z10) - z10)/(z2 - (1.d0,0.d0)) ! Верх. полуплоскость z3
		z4 = (/z3(1),z3(4),z3(5),z3(8),z3(9),z3(10)/)
		x4 = dreal(z4) ! Вектор коорд.краевых точек проводников по оси х4
	call GHIONE2(x4,dC,n,5000) ! Копланар.полоски (Coplanar Strips - CPS) М=5000!	
		   bC01=dC(1)+dC(2); bC02=dC(3)+dC(4);  bCc=bC01+bC02; bCp=dC(1); 
		   bC12=-dC(2);      bC11=bC01+bC12;    bC22=bC02+bC12 ! Частич.емкост.
! Удвоение емкости в полной структуре			
		dC=2*dC		   ! Матрица емкостей структуры СЛ при воздушном заполнении
!------------------------------------------------------------------------------				
! Результирующая матрица индуктивностей (мкГн/м)
	call induc(dC,dL,n)
!		print*,'Inductance matrix [L] (uH/m)'
!		call dprint(dL,n)
!------------------------------------------------------------------------------				
! Случай однородного диэлектрика е1 = е2
	if(e1==e2) then; dC=8.854*dC; return; endif
!==============================================================================
! Расчет z1in границ раздела диэлектриков в областях z1,z2,z3(cp),z4(cp)
! Сквозное отображение областей     zz1 -> (zz2  =  zz3 ) -> zz4
! Границы синфазной области		     z1 -> ( z2  =  z3с ) -> z4с
! Границы противофазной области		 z1 -> ( z2  =  z3p ) -> z4p
! Внутренность синфазной области   z1in -> (z2in = z3inc) -> z4cin 
! Внутренность противофаз.области  z1in -> (z2in = z3inp) -> z4pin 	
! В оригинале обозначено (без син./противо.) [w1 <- z1 = z2 -> w2]
!******************************************************************************
! Отображ. исход.области z1 -> диск z2 -> синфаз.диск z3c -> синфаз.прямоугольник z4c
	 betam4 =(/-.5,-.5,-.5,-.5/)      !! Задание прямых углов при вершинах 1,2,3,4 	
! Отображение диска z2 на диск z3с (перенум. точек). Поиск вершин прямоугольника z4с
	 z3c = 	(/z2(1),z2(8),z2(9),z2(10)/) 
		call qinit(4,betam4,nq,qwork4)!! Инициализация раб.массивов: betam4,nq,qwork4
		c4 = (1.,0.)				  !! 		
	 do 2 k=1,4
2    z4c(k)=WSC(z3c(k),k, zero,zero,0, 4, c4, z3c, betam4,nq, qwork4)
!------------------------------------------------------------------------------	
! Отображение zz1 --> zz4c в цикле do21 при синфазном возбуждении 
	 do 21 i=1,inn  
	 ier = 0  ! ier - признак ошибки ZSC(вх/вых). ier=1 подавить сообщения об ошиб.!!
	 z2in(i)= ZSC(z1in(i),0,zero, zero,z10,0,tol,ier, nn, c, z2,z10,z1,betam,nq,qwork)!! 
	 if(ip==0) write(*,111) i, z1in(i), z2in(i), ier			
111  format(i2,' zz1 -> zz2 =', 2(' (',f9.6,',',f9.6,')'),i2) 
! Внутренние точки z4cin в финальном прямоугольнике z4c 
	 zz4 = WSC(z2in(i),0,zero,zero,0, 4, c4, z3c, betam4,nq, qwork4)
21	 z4cin(i)=(zz4 - z4c(2))/(z4c(3) - z4c(2)) ! Линейное преобраз.-нормировка zz4c
	    z4c = (z4c - z4c(2))/(z4c(3) - z4c(2)) ! Линейное преобраз.-нормировка z4c
!------------------------------------------------------------------------------	
  	 if(ip==0) then; write(*,112) (z4c(k),k=1,4)
112  format(/' Vertices of final rectangle z4c:',4(/4x'(',f8.4,',',f8.4,')'))
	 print*,'Points in final rectangle z4cin:'; write(*,113)(i,z4cin(i),i=1,inn)
113  format(i3,1x,'(',f8.4,',',f8.4,')'); endif
!******************************************************************************
! Отображ. исход.области z1 -> диск z2 -> противофаз.диск z3р -> прямоугольник z4р
! Отображение диска z2 на диск z3p(перенум.точек). Поиск вершин прямоугольника z4p
	 z3p = 	(/z2(1),z2(4),z2(5),z2(10)/) 
	 do 3 k=1,4
3    z4p(k)=WSC(z3p(k),k, zero,zero,0, 4, c4, z3p, betam4,nq, qwork4)
!------------------------------------------------------------------------------	
! Отображение zz1 --> zz4p в цикле do22 при противофазном возбуждении 
	do 22 i=1,inn  
! Внутренние точки z4pin в финальном прямоугольнике z4p 
	 zz4 = WSC(z2in(i),0,zero,zero,0, 4, c4, z3p, betam4,nq, qwork4)
22	 z4pin(i)=(zz4 - z4p(2))/(z4p(3) - z4p(2)) ! Линейное преобраз.-нормировка zz4p
	    z4p = (z4p - z4p(2))/(z4p(3) - z4p(2)) ! Линейное преобраз.-нормировка z4p
!------------------------------------------------------------------------------
	 if(ip==0) then; write(*,114) (z4p(k),k=1,4)
114  format(/' Vertices of final rectangle z4p:',4(/4x'(',f8.4,',',f8.4,')'))
	 print*,'Points in final rectangle z4pin:'; write(*,115)(i,z4pin(i),i=1,inn)	
115  format(i3,1x,'(',f8.4,',',f8.4,')'); endif
!==========================================================================
! Точки интереса на границах раздела диэл. в финальных прямоугольниках z4с, z4p 
	x4cin = real(z4cin); x4ci=x4cin((/1,2,3,4,5/)) ; !print*, ' x4ci=', x4ci !,'\n'c
	x4pin = real(z4pin); x4pi=x4pin((/1,2,3,4,5/)) ; !print*, ' x4pi=', x4pi !,'\n'c
	y4cin = imag(z4cin); y4ci=y4cin((/1,2,3,4,5/)) ; !print*, ' y4ci=', y4ci !,'\n'c
	y4pin = imag(z4pin); y4pi=y4pin((/1,2,3,4,5/)) ; !print*, ' y4pi=', y4pi !,'\n'c
	yoc  = y4cin(6);	if(ip==0)print*,' yoc =',yoc ! Граница раздела С01 и С02
	yop  = y4pin(7);	if(ip==0)print*,' yop =',yop ! Граница раздела С01 и С12
!---------------------------------------------------------------------------
! Нормированные емкости С
	if(ip==0)print*,'\n Normalized capacitances C(-):	      True:'c
	aCc = imag(z4c(1));	   if(ip==0)print*,' Cc =', aCc,  bCc   
	aCp = imag(z4p(1));	   if(ip==0)print*,' Cp =', aCp,  bCp   
	aC01c=aCc - yoc;	   if(ip==0)print*,' C01c=',aC01c,bC01 ! менее точно 
	aC01p=aCp - yop;	   if(ip==0)print*,' C01p=',aC01p,bC01 ! точнее 
	aC02= yoc;			   if(ip==0)print*,' C02=', aC02, bC02  
	aC11= aCp;			   if(ip==0)print*,' C11=', aC11, bC11  
	aC22= aCc+aCp-2*aC01p; if(ip==0)print*,' C22=', aC22, bC22 
	aC12= aCp - aC01p;	   if(ip==0)print*,' C12=', aC12, bC12;if(ip==0)print* 
!==========================================================================
! Угловые и экстремальные точки диэлектрика в С- и Р- прямоугольниках z4c, z4p
	xmac = maxval(x4ci);   if(ip==0)print*,'xmac =', xmac
	ymic = minval(y4ci);   if(ip==0)print*,'ymic =', ymic 
	ymip = minval(y4pi);   if(ip==0)print*,'ymip =', ymip
!---------------------------------------------------------------------------
! Собственная частичная емкость dC02 с учетом диэл. заполнения е1 и е2
	dc1c = e2 * ymic;				if(ip==0)print*, 'dc1c =', dc1c
	b = yoc-ymic
	eef=e1
	if(e1/=e2)eef=e1*e2*log(e1/e2)/(e1-e2) ! эфф.прониц. наклон.-совмещ.диэл.	
	dc12c= eef* b / xmac
	dc22c= e2 * b /(1.-xmac)
	dc2c = 1./(1./dc12c +1./dc22c); if(ip==0)print*, 'dc2c =', dc2c
	dC02 = dc1c + dc2c;				if(ip==0)print*, 'dC02 =', dC02
! Взаимная емкость dC12 с учетом диэл. заполнения е1 и е2
	dc1p = e1 * ymip;				if(ip==0)print*, 'dc1p =', dc1p
	dc2p = eef*(yop - ymip);		if(ip==0)print*, 'dc2p =', dc2p
	dC12 = dc1p + dc2p;				if(ip==0)print*, 'dC12 =', dC12
!---------------------------------------------------------------------------
! Матрица емкостей с учетом диэлектрического заполнения е1 и е2
		dC(1) = aC01p + dC12
		dC(2) = -dC12
		dC(3) = dC(2)
		dC(4) = dC02 + dC12
! Удвоение емкости в полной структуре и введение размерности (pF/m)			
		dC=2*8.854*dC	
!==========================================================================
	 	return
		end 
